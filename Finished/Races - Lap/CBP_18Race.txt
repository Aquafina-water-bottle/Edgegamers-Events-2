//#-p stonebrick 0 -print
///	232 26 -200

//<Defines
DEFINE $s$ minecraft:
DEFINE $ObjA$ scoreboard objectives add
DEFINE $ObjR$ scoreboard objectives remove
DEFINE $TeamA$ scoreboard teams add
DEFINE $TeamO$ scoreboard teams option
DEFINE $TeamR$ scoreboard teams remove
DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"PARSE($TD$)","color":"PARSE($Color$)","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"1.8 Race","color":"PARSE($Color$)"}}},{"text":"]","color":"gray"},{"text":": "},

DEFINE Deactivate fill |0| stonebrick 0
DEFINE Activate fill |0| redstone_block 0
DEFINE ScTG scoreboard players tag
DEFINE ScOP scoreboard players operation
DEFINE RevST scoreboard players tag @a[|0|] remove |0|
DEFINE SetblockTestfor setblock ~ ~ ~ repeating_command_block 11 replace {TrackOutput:0b,auto:1b}
DEFINE NumberAdd |0| Number = |0|
DEFINE NumberObjAdd Number dummy

DEFINE $Style$ 0 10 10
DEFINE $SA$ x=212,y=7,z=-241,dx=129,dy=100,dz=50

///	Name
DEFINE $Name$ 18
///	Race Status Number
DEFINE $RSn$ 10
///	Warp name
DEFINE $WN$ 18r
///	Text display name
DEFINE $TD$ 1.8R
///	Scoreboard teams color
DEFINE $Color$ gold
///	Place where air is when open
DEFINE $Dao$ 249 24 -225 255 24 -219
///	Place where redstone blocks are there when open
DEFINE $Dro$ 248 22 -221 255 22 -223
///	Spawn Coords for teleporting
DEFINE $SC$ 242 26 -222 -90 0
///	Spawn Coords ID
DEFINE $SPid$ 11

///	Where laps are completed
DEFINE $lap$ x=249,y=25,z=-229,dx=6,dy=5,dz=2
///	Area where laps are reset inside the races
DEFINE $lapAF$ x=269,y=16,z=-227,dx=8,dy=2,dz=4
///	Area where laps are disabled in spawn
DEFINE $lapSP$ x=240,y=24,z=-223,dx=31,dy=4,dz=2

///	Sand Randomizer Coords
DEFINE $SandCoordsX$ 249
DEFINE $SandCoordsY$ 24
DEFINE $SandCoordsZ$ -215

///	Load Glass Structure Block Coords
DEFINE $LoadGlass$ 257 22 -211

///	Load Glass Region to replace with barrier
DEFINE $LoadGlassRegion$ 257 27 -211 288 27 -205

///	Poison area
DEFINE $PoisonArea$ x=263,y=14,z=-221,dy=4
///	Teleport player from fence gate - Region
DEFINE $TpRg$ x=256,y=26,z=-238,dy=1
///	Teleport player to area from fence gate
DEFINE $TpTo$ 250 26 -238 0 0

///	Teleport player from underwater area
DEFINE $UnderwaterTP$ 264 16 -226 -90 0

///	Summon gold armor stands
DEFINE $UnderwaterGold1$ 260 20 -216
DEFINE $UnderwaterGold2$ 267 20 -213
DEFINE $UnderwaterGold3$ 270 20 -215
DEFINE $UnderwaterGold4$ 264 20 -218
DEFINE $UnderwaterGold5$ 274 20 -229
DEFINE $UnderwaterGold6$ 270 20 -227
DEFINE $UnderwaterGold7$ 266 20 -228
DEFINE $UnderwaterGold8$ 262 20 -228
DEFINE $UnderwaterGold9$ 261 20 -221
DEFINE $UnderwaterGold10$ 257 20 -228

///	Gold armor stand
DEFINE $ArmorStandGold$ {Tags:["18Race","18RaceGold"],DisabledSlots:2096896,Invulnerable:1,PersistenceRequired:1,Invisible:1,NoGravity:1,ArmorItems:[{},{},{},{id:gold_block,Count:1b}]}

//>

//<Comments
FUNC Func_Comments
1.8r

Clocks:

-1 teleport
-1 that retains invisibility
-1 that kills you on the sand
-1 that kills you when you are 2 blocks above any hay

-1 that loops around the 5 sand layouts - every 10s (200mt)
-1 that will switch around 5 different barrier layouts, connected to the one for the sand

-1 that gives poison to the player every 1/5 time for 20 seconds

-1 that changes the layout of the underwater area every minute - 1200mt
	-Has countdown with an invisible armor stand from 5-1
	-Turns on redstone lamp when it is at 5
	-Playsounds an explosion on where the original closed, and another softer sound to where it opened

260 20 -216
267 20 -213
270 20 -215
264 20 -218
274 20 -229
270 20 -227
266 20 -228
262 20 -228
261 20 -221
257 20 -228


END
//>

//<To Do 
FUNC Func_ToDoThings
TDWorks TDTest TDFix
Mandatory Unimplemented Changes:
	-
	
General Unimplemented Changes:
	-
	
Bugs:
	-
	
Implemented Changes:
	-
	
Bug fixes:
	-
	
END
//>


//<Main Start
FUNC Func_GeneralScoreboardStart
///	Scoreboards
	USE $ObjA$
		18Race dummy 1.8 Race
		18RaceCD dummy 1.8 Race Countdown
		18RaceTimer1 dummy 1.8 Race Timer - 40
		18RaceTimer2 dummy 1.8 Race Timer - 200
		18RaceTimer3 dummy 1.8 Race Timer - 80
		18RaceRand dummy 1.8 Race Randomizer
		18RaceTP dummy 1.8 Race Teleport
	UNUSE
	scoreboard objectives setdisplay sidebar $Name$Race
	
	scoreboard teams add $Name$R
	scoreboard teams option $Name$R color $Color$
	scoreboard teams option $Name$R seeFriendlyInvisibles true
	scoreboard teams option $Name$R collisionRule never
	scoreboard teams option $Name$R friendlyfire false
	
///	Race Status
	scoreboard objectives add RS dummy Race Status
	scoreboard teams add RSg Race Status Green
	scoreboard teams add RSr Race Status Red
	scoreboard teams option RSg color green
	scoreboard teams option RSr color red
	
	scoreboard teams join RSg $Name$R_Closed
	scoreboard teams join RSr $Name$R_Open
	$Name$R_Open RS = $RSn$
	scoreboard players reset $Name$R_Closed RS
	
///	Countdown start
	summon AreaEffectCloud ~ ~ ~ {Duration:130,Tags:["$Name$RaceCD"]}
	
///	Other
	effect @a[$SA$] clear
	\/warp_$WN$ $Name$Race = 99
	scoreboard teams join $Name$R /warp_$WN$
	gamerule naturalRegeneration false
	
///	Timer Armor Stand
	summon ArmorStand ~ ~ ~ {Tags:["18Race","18RaceTimer"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	
	@e[type=ArmorStand,18RaceTimer] 18RaceTimer1 + 0
	@e[type=ArmorStand,18RaceTimer] 18RaceTimer2 + 0
	
///	Sand Randomizer
	LOOP 1 5 + 1 () 0 4 + 1
		summon AreaEffectCloud $SandCoordsX$ SUB($SandCoordsY$;|2|) $SandCoordsZ$ {Tags:["18Race","18RaceRand","18RaceSand","18RaceSand|0|"],Duration:1000000}
	END
	
///	Golden Block Randomizer
	LOOP 1 10 + 1
		summon AreaEffectCloud $UnderwaterGold|0|$ {Tags:["18Race","18RaceRand","18RaceWater","18RaceWater|0|"],Duration:1000000}
	END
	
	@e[type=AreaEffectCloud,18RaceRand] 18RaceRand + 0
	@r[type=AreaEffectCloud,18RaceWater] 18RaceRand = 1
	
END
//>

//<Sand Randomizer Pulse
FUNC Func_SandRandomizerPulse
	@r[type=AreaEffectCloud,18RaceSand,18RaceRand=0] 18RaceRand = 1
	@e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] setblock ~ ~ ~ redstone_block 0
	@e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] setblock ~ ~ ~ stonebrick 0
	@e[type=AreaEffectCloud,18RaceSand,18RaceRand=2] 18RaceRand = 0
	@e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] 18RaceRand = 2
END
//>

//<Main Clock
FUNC Func_GeneralScoreboardClock
///	General
	$s$weather clear 1000000
	$s$gamemode 2 @a[$SA$,m=0]
	$s$clear @a[$SA$,m=2]
	@a[$SA$,team=!$Name$R] SPid + 0
	@a[$SA$,team=!$Name$R,SPid<-$SPid$] SPid = $SPid$
	@a[$SA$,team=!$Name$R,SPid>-$SPid$] SPid = $SPid$
	scoreboard teams join $Name$R @a[$SA$,team=!$Name$R]
	effect @a[$SA$,m=2,team=$Name$R] 14 3 0 true
	effect @a[$SA$,m=2,team=$Name$R] 23 3 0 true
	
///	Laps
	@a[$SA$,m=2,team=$Name$R] $Name$Race + 0
	ScTG @a[$lapSP$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] add $Name$Lap
	@a[$lap$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] $Name$Race + 1
	@a[$lap$,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap] tellraw @a[$SA$] $TextStart${"selector":"@a[c=1,r=0,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap]"},{"text":" has finished a lap!","color":"yellow"}]}
	@a[$lap$,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap] playsound minecraft:entity.experience_orb.touch voice @a
	ScTG @a[$lap$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] add $Name$Lap
	ScTG @a[$lapAF$,m=2,team=$Name$R,$Name$Race>=0,$Name$Lap] remove $Name$Lap
	
///	Teleport
	@a[$TpRg$,m=2,team=$Name$R] 18RaceTP = 2
	$s$tp @a[$SA$,m=2,team=$Name$R,18RaceTP=2] $TpTo$
	@a[$SA$,m=2,team=$Name$R] sand -1 ~ ~-0.2 ~ $s$kill @a[c=1]
	
///	Poison
	@e[type=ArmorStand,18RaceTimer,18RaceTimer1>=0<=39] 18RaceTimer1 + 1
	@e[type=ArmorStand,18RaceTimer,18RaceTimer1=40] effect @a[$PoisonArea$,m=2,team=$Name$R] 19 20 3 true
	@e[type=ArmorStand,18RaceTimer,18RaceTimer1=40] 18RaceTimer1 = 0
	
///	Change sand and barrier block formation
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2>=0<=199] 18RaceTimer2 + 1
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=186] fill $LoadGlassRegion$ air 0
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=187] setblock $LoadGlass$ redstone_block 0
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=187] setblock $LoadGlass$ stonebrick 0
	
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=192] fill $LoadGlassRegion$ barrier 0 replace glass 0
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=196] fill $LoadGlassRegion$ glass 0 replace barrier 0
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] fill $LoadGlassRegion$ barrier 0 replace glass 0
	
	testfor @e[type=ArmorStand,18RaceTimer,18RaceTimer2=200]
	CONDITION
		Activate(CBPSandPulse)
	@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] 18RaceTimer2 = 0
	
///	Golden block Randomizer
	$s$tp @e[type=ArmorStand,18RaceGold] ~ ~ ~ ~2 ~
	@a[$SA$,m=2,team=$Name$R] @e[r=1,c=1,type=AreaEffectCloud,18RaceRand=2] 18RaceRand = 3
	
	@e[type=AreaEffectCloud,18RaceRand=3] @a[r=1,c=1,m=2,team=$Name$R] 18RaceTP = 1
	$s$tp @a[$SA$,m=2,team=$Name$R,18RaceTP=1] $UnderwaterTP$
	
	@e[type=AreaEffectCloud,18RaceRand=3] ~ ~-1 ~ $s$kill @e[r=1,c=1,type=ArmorStand,18RaceGold] $UnderwaterTP$
	@e[type=AreaEffectCloud,18RaceRand=3] @r[type=AreaEffectCloud,18RaceRand=0] 18RaceRand = 1
	@e[type=AreaEffectCloud,18RaceRand=3] 18RaceRand = 0
	
	@e[type=AreaEffectCloud,18RaceRand=1] summon ArmorStand ~ ~-1 ~ $ArmorStandGold$
	@e[type=AreaEffectCloud,18RaceRand=1] playsound minecraft:entity.experience_orb.touch voice @a[$SA$]
	@e[type=AreaEffectCloud,18RaceRand=1] 18RaceRand = 2
	
///	Regeneration
	effect @a[$SA$,m=2,team=$Name$R] 10 0 0 true
	@a[$SA$,m=2,team=$Name$R] 18RaceTimer3 + 1
	effect @a[$SA$,m=2,team=$Name$R,18RaceTimer3=80] 10 1 10 true
	@a[$SA$,m=2,team=$Name$R,18RaceTimer3=80] 18RaceTimer3 = 0
	
///	TP End
	@a[$SA$,m=2,team=$Name$R,18RaceTP=1] playsound minecraft:entity.player.levelup voice @a[c=1]
	@a[$SA$,m=2,team=$Name$R,18RaceTP=2] playsound minecraft:entity.endermen.teleport voice @a[c=1]
	@a[$SA$,m=2,team=$Name$R,18RaceTP>=1] particle happyVillager ~0.1 ~1 ~0.1 0.3 0.5 0.3 1 20 force
	@a[$SA$,m=2,team=$Name$R,18RaceTP>=1] 18RaceTP = 0
END
//>

//<Countdown
FUNC Func_CountdownClock
	@e[type=AreaEffectCloud,$Name$RaceCD] $Name$RaceCD + 1
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=20] tellraw @a[$SA$] $TextStart${"text":"5","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=20] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 0.5
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=40] tellraw @a[$SA$] $TextStart${"text":"4","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=40] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 1
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=60] tellraw @a[$SA$] $TextStart${"text":"3","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=60] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 1.5
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=80] tellraw @a[$SA$] $TextStart${"text":"2","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=80] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 2
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=100] tellraw @a[$SA$] $TextStart${"text":"1","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=100] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 2
	
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] @a[$SA$] playsound minecraft:entity.wither.death voice @a[c=1]
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] tellraw @a[$SA$] $TextStart${"text":"GO!","color":"green","bold":"true"}]}
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] title @a[$SA$] title {"text":"GO!","color":"green"}
	testfor @e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120]
	CONDITION
		Deactivate(CBPCountdown)
END
//>

//<Main End
FUNC Func_GeneralScoreboardEnd
///	Scoreboard
	USE $ObjR$
		18Race
		18RaceCD
		18RaceTimer1
		18RaceTimer2
		18RaceTimer3
		18RaceRand
	UNUSE
	scoreboard teams remove $Name$R
	ScTG * remove $Name$Lap
	
///	Custom
	$s$kill @e[type=ArmorStand,18Race]
	$s$kill @e[type=AreaEffectCloud,18Race]
	
///	Race Status
	$Name$R_Closed RS = $RSn$
	scoreboard players reset $Name$R_Open RS
	
///	Countdown reset
	$s$kill @e[$Name$RaceCD]
	
///	General reset
	gamerule naturalRegeneration true
	effect @a[$SA$,m=2] clear
	$s$clear @a[$SA$,m=2]
	effect @a[$SA$,m=2] 6 20 100 true
	$s$tp @a[$SA$,m=2] $SC$
END
//>


//<Groups
:CBPMainStart PULSE $Style$
Deactivate(CBPMainStart)
Func_GeneralScoreboardStart
Activate(CBPMainClock)
Activate(CBPCountdown)

:CBPMainClock CLOCK $Style$
Func_GeneralScoreboardClock

:CBPSandPulse PULSE $Style$
Deactivate(CBPSandPulse)
Func_SandRandomizerPulse
	
:CBPCountdown CLOCK $Style$
Func_CountdownClock

	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $Dao$ stonebrick 0 replace redstone_block 0
	@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $Dro$ redstone_block 0 replace stonebrick 0

:CBPMainEnd PULSE $Style$
Deactivate(CBPMainEnd)
Func_GeneralScoreboardEnd
Deactivate(CBPMainClock)
Deactivate(CBPCountdown)
	
	fill $Dao$ redstone_block 0 replace stonebrick 0
	fill $Dro$ stonebrick 0 replace redstone_block 0
//>














